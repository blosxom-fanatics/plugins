# Blosxom Plugin: breadcrumbs
# Author(s): Rael Dornfest <rael@oreilly.com>
# Version: 2003-12-29
# Documentation: See the bottom of this file or type: perldoc readme

package breadcrumbs;
use strict;

# --- Configurable variables -----

# Should I prepend a link back home to the path?
# Specify a word to use for the link back to $blosxom::url or leave blank
# to exclude it

my $home = '';

# What divider should I use between path components (e.g. a > path > to...)
my $divider = ' | ';

# --------------------------------

our $breadcrumbs; # use as $breadcrumbs::breadcrumbs in flavour templates
our $title;

use FileHandle;

my $fh = FileHandle->new;

sub start { 1 }

sub head {
    my ($pkg, $path, $head_ref) = @_;

    $title = $blosxom::blog_title;

    return 0 unless $path;


    ( my $dir = $path ) =~ s{/[^/]+[.]\Q$blosxom::flavour\E$}{};

    my @p = $home
        ? (qq{<a href="$blosxom::url/">$home</a>})
        : ();

    my @q;

    my $p = '/';
    for my $part ( split m{/}, $dir ) {
        $p .= "$part/";
        my $name = ucfirst join ' ', map { s/sql/SQL/g; ucfirst } split /_+/, $part;
        push @p, qq{<a href="$p">$name</a>};
        push @q, $name;
    }

    $breadcrumbs = join $divider, @p;

    if ($path =~ m#(.*?)/?([\-\.\w]+)\.(\w+)$# and $2 ne 'index') {
        my $file = join('/', $blosxom::datadir, $1, "$2.$blosxom::file_extension");
        if (-f $file) {
            open my $fh, '<', $file or die "Cannot open $file: $!\n";
            chomp(my $t = <$fh>);
            close $fh or die "Cannot close $file: $!\n";
            $title .= ": $t";
        }
    } else {
        $title .= ': ' . join $divider, @q;
    }

    return 1;
}

1;

__END__

=head1 NAME

Blosxom Plug-in: breadcrumbs

=head1 SYNOPSIS

Populates $breadcrumbs::breadcrumbs with a clickable trail to
your current path in the weblog hierarchy (a la Yahoo!).

e.g. a/path/to/somewhere becomes
<a href="/a">a</a> :: <a href="/a/path">path</a> :: ...

Optionally prepends the path with a link back to home.  Alter $home
as you please, leaving it blank to turn off the link to home.

=head1 VERSION

2003-12-29

Version number coincides with the version of Blosxom with which the
current version was first bundled.

=head1 AUTHOR

Rael Dornfest  <rael@oreilly.com>, http://www.raelity.org/

=head1 SEE ALSO

Blosxom Home/Docs/Licensing: http://www.raelity.org/apps/blosxom/

Blosxom Plugin Docs: http://www.raelity.org/apps/blosxom/plugin.shtml

=head1 BUGS

Address bug reports and comments to the Blosxom mailing list
[http://www.yahoogroups.com/group/blosxom].

=head1 LICENSE

Blosxom and this Blosxom Plug-in
Copyright 2003, Rael Dornfest

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the "Software"),
to deal in the Software without restriction, including without limitation
the rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.
